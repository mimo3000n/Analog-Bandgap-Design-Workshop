# Analog-Bandgap-Design-Workshop
Analog Bandgap Design Workshop

<img width="1290" height="706" alt="image" src="https://github.com/user-attachments/assets/68e851b2-2181-4659-89c1-fa8d3daba25a" />

## Overview


Bandgap Voltage Reference (BGR) Design and Characterization is a hands-on workshop for mastering one of the most critical analog circuits. Built on industry-standard principles, you will work through the complete design flow: from architecture selection and component sizing to full-circuit simulation and verification. The curriculum emphasizes a deep understanding of BGR fundamentals, robust design techniques, and credible verification methods that meet international standards for research and product development.

 

You will begin at the component level with CTAT and PTAT voltage generation circuits, understanding the physics of bipolar transistors and the design of precision resistors. You will then construct and analyze a self-biased current mirror, tackling issues of supply independence and implementing a reliable start-up circuit. Each module includes practical calculations, pre-verified netlist templates, and measurement scripts to ensure you focus on design intuition rather than tool debugging.

 

The program culminates in the integration of a complete BGR circuit. You will simulate and analyze the final output for temperature stability (tempco), line regulation (PSRR), and load regulation. Guided by detailed specifications, you will run deterministic PVT corners (Process, Voltage, Temperature) to sign-off on the designâ€™s robustness. Graduates leave with a coherent portfolio: annotated schematics, simulation results, and a comprehensive engineering report that demonstrates readiness for analog and mixed-signal design roles.

------------------------------------------------------------------------
## Bandgap design theory

&nbsp;
### Lec0 Opening Lecture
&nbsp;

<img width="1766" height="954" alt="image" src="https://github.com/user-attachments/assets/e2e503f6-2764-4919-9b25-807bd5dd46bc" />

&nbsp;
<img width="1759" height="952" alt="image" src="https://github.com/user-attachments/assets/8d4ea606-d8c8-4801-85fa-ae445b5d8fcd" />
&nbsp;

&nbsp;
<img width="1763" height="954" alt="image" src="https://github.com/user-attachments/assets/8184d065-9627-43e1-90f3-7396ffe2ce75" />
&nbsp;

&nbsp;
<img width="1757" height="953" alt="image" src="https://github.com/user-attachments/assets/f9e6e038-0ecc-4d06-a79f-fb214cfcbdab" />
&nbsp;
<img width="1760" height="953" alt="image" src="https://github.com/user-attachments/assets/d7fa1aad-eb34-4a50-a778-465ade4968e8" />
&nbsp;

&nbsp;
<img width="1761" height="951" alt="image" src="https://github.com/user-attachments/assets/bcd97b3a-e6f6-40c8-b816-d42b92e747f1" />
&nbsp;

&nbsp;
### Lec1 Introduction to BGR
&nbsp;

&nbsp;
<img width="1757" height="996" alt="image" src="https://github.com/user-attachments/assets/139681d8-359e-4650-9f64-93c0991b4711" />

<img width="1762" height="993" alt="image" src="https://github.com/user-attachments/assets/2df41e7a-adc9-4c0f-a4bb-58194da0bafc" />

<img width="1763" height="992" alt="image" src="https://github.com/user-attachments/assets/b0a63ffc-dc83-4d46-932c-fb402ac8f2d2" />

<img width="1758" height="994" alt="image" src="https://github.com/user-attachments/assets/c61aaee6-37d1-4b96-9bac-138e447408c1" />

<img width="1765" height="993" alt="image" src="https://github.com/user-attachments/assets/d5577235-2a14-43e2-95f1-d1c10d042b22" />

<img width="1764" height="994" alt="image" src="https://github.com/user-attachments/assets/be79ea3f-bc41-4fbc-a84c-ba183d9b4905" />

<img width="1760" height="988" alt="image" src="https://github.com/user-attachments/assets/017df5db-5b91-44f0-b3f1-b712c541df8e" />


<img width="1763" height="990" alt="image" src="https://github.com/user-attachments/assets/5f8feb94-6a9d-443e-add0-056b59eb54b1" />

<img width="1761" height="987" alt="image" src="https://github.com/user-attachments/assets/87be0849-7ba3-42e7-bc3c-bafd07ccdb18" />

<img width="1770" height="992" alt="image" src="https://github.com/user-attachments/assets/a0b89418-c837-4c81-b58d-50ba513ef005" />

<img width="1763" height="991" alt="image" src="https://github.com/user-attachments/assets/bc8bdb52-1199-4888-bdfc-afdcc126f4f9" />



### Lec2 CTAT Voltage generation Circuit


<img width="1763" height="992" alt="image" src="https://github.com/user-attachments/assets/b34a1680-11c8-4305-b13d-5bf49f73476c" />


###	Lec3 PTAT Voltage generation Circuit

###	Lec4 Biased Current Mirror Circuit

###	Lec5 Reference Voltage branch Circuit

###	Lec6 Start-up Circuit

###	Lec7 Complete BGR Circuit

------------------------------------------------------------------------------------------
## Bandgap design labs using Sky130	

&nbsp;
### Tools and PDK Setup
&nbsp;

<img width="1762" height="990" alt="image" src="https://github.com/user-attachments/assets/e16c1a3c-b312-42d8-990e-1efc7ade5e7a" />

<img width="796" height="266" alt="image" src="https://github.com/user-attachments/assets/4a256ace-f221-4ea2-bb62-210e1114b6b7" />

<img width="740" height="237" alt="image" src="https://github.com/user-attachments/assets/4bc6d88e-6f90-40fe-a616-fc08845d2620" />

<img width="1280" height="809" alt="image" src="https://github.com/user-attachments/assets/4c32b6ea-ee5a-42fe-a6fe-7780d854a929" />

<img width="1284" height="806" alt="image" src="https://github.com/user-attachments/assets/21b44d01-01c7-4fcb-949f-e1359923ad6d" />




### Design spec, Device data, Design Step
&nbsp;

<img width="1760" height="991" alt="image" src="https://github.com/user-attachments/assets/02038e43-d99b-4093-af8e-918d8ca8a528" />

<img width="1761" height="990" alt="image" src="https://github.com/user-attachments/assets/018b9e83-fbc4-4177-8d52-b9e577490f8b" />

<img width="1762" height="994" alt="image" src="https://github.com/user-attachments/assets/9f798c33-87c6-41f5-8348-80e5c8340347" />

<img width="1763" height="991" alt="image" src="https://github.com/user-attachments/assets/48005fcc-6133-428d-9209-1a737265b151" />

<img width="1766" height="991" alt="image" src="https://github.com/user-attachments/assets/1545896a-b736-4e88-b52b-bb4f7d8e647d" />

<img width="1765" height="992" alt="image" src="https://github.com/user-attachments/assets/9eedb5d6-0045-4f10-9d07-9e9c264bbf83" />

<img width="1766" height="990" alt="image" src="https://github.com/user-attachments/assets/d6d51041-4e75-4426-92cf-5baa24476f1f" />

<img width="1765" height="991" alt="image" src="https://github.com/user-attachments/assets/26c78286-f3d4-448a-bbbd-614f9698fd29" />











###	component design with spice netlist
&nbsp;

###	CTAT design and prelayout simulation
&nbsp;

<img width="1321" height="744" alt="image" src="https://github.com/user-attachments/assets/45b4bd6f-b38d-4f53-9b47-df7674f0c438" />

<img width="1323" height="747" alt="image" src="https://github.com/user-attachments/assets/b1c0e4de-3288-4a5b-ae6a-57a03e2103e9" />
&nbsp;


Setup of wsl

will be documented!!

ctat_voltage_gen.sp spice file

``` spice
*CTAT Voltage generation with single BJT

.lib /home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice tt

.global vdd gnd
.temp 27

**** bjt definition
xqp1    gnd     gnd     qp1     sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1

*** supply voltage and current
vsup    vdd     gnd     dc      2
isup    vdd     qp1     dc      10u
.dc     temp    -40         125       5

*** control statement
.control
run
plot v(qp1)
.endc
.end

```

runing spice simulation with following command:

ngspice ctat_voltage_gen.sp

<img width="792" height="502" alt="image" src="https://github.com/user-attachments/assets/6976cefe-d87b-4e59-979e-f6f49370966c" />

plot result

<img width="1137" height="748" alt="image" src="https://github.com/user-attachments/assets/741347ac-6e7c-4956-9712-a497049bca6b" />


slope:

<img width="622" height="115" alt="image" src="https://github.com/user-attachments/assets/8b8ea151-3836-4772-b885-4c81d4795364" />

CTAT Voltage generation with different current source values

``` spice
*CTAT_ckt Voltage generation with different current source values
.lib /home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice ss
vdd     d        0          1.8
I0      d        ctat_op    10u
xpq1    0        0          ctat_op   sky130_fd_pr__pnp_05v5_W3p40L3p40   m=8
.dc     temp    -40         125       5      I0    1u    10u      1u
.control
run
plot v(ctat_op)
.endc
.end

```

simulation with ngspice:

<img width="781" height="483" alt="image" src="https://github.com/user-attachments/assets/aa25a86f-4b6c-424f-b8ae-7271c7d54795" />

plot result:

<img width="1277" height="956" alt="image" src="https://github.com/user-attachments/assets/e02be5cc-3c57-4998-bafd-218b8ac0a6a0" />













###	PTAT design and prelayout simulation
&nbsp;

next we do simulation on following ciruit.

<img width="880" height="499" alt="image" src="https://github.com/user-attachments/assets/16eaffaf-cdfc-4ecc-8f57-75842756158b" />

``` spice
**** ptat voltage generation circuit *****

.lib "/home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice tt"

.global vdd gnd
.temp 27

*** vcvs definition
e1      net2    gnd     ra1     qp1     gain=1000
xmp1    q1      net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp2    q2      net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4

*** bjt definition
xqp1    gnd     gnd     qp1             sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1
xqp2    gnd     gnd     qp2          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=8

*** high-poly resistance definition
xra1    ra1     na1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra2    na1     na2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra3    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra4    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8

*** voltage sources for current measurement
vid1    q1      qp1     dc      0
vid2    q2      ra1     dc      0

*** supply voltage
vsup    vdd     gnd     dc      2
.dc     temp    -40     125     5

*** control statement
.control
run
plot v(qp1) v(ra1) v(qp2) v(net2)
plot vid1#branch vid2#branch
.endc
.end

```

simulation with ngspice:

<img width="775" height="522" alt="image" src="https://github.com/user-attachments/assets/a46b757e-6d4f-423d-b677-333f2524c40e" />

plot results:

<img width="1228" height="855" alt="image" src="https://github.com/user-attachments/assets/613500f8-4ba6-4539-ae66-17235c999417" />

<img width="1218" height="759" alt="image" src="https://github.com/user-attachments/assets/32a6d3c0-47b4-4176-94de-b23f12b7af80" />


plot v(qp1) & v(ra1) and see that there is no difference alsoslop is the same

ngspice command: plot v(qp1) v(ra1)

<img width="411" height="57" alt="image" src="https://github.com/user-attachments/assets/baf87960-f011-4f18-8c39-03b196b4503b" />

plot result: 

<img width="1169" height="819" alt="image" src="https://github.com/user-attachments/assets/ee67f335-be0b-4ed6-bca8-c04c95bff6a7" />

now we run "plot v(qp2) v(ra1)

<img width="325" height="56" alt="image" src="https://github.com/user-attachments/assets/8aec85ca-8d97-4e8f-bc96-e682b10599b1" />

plot result:

<img width="1300" height="855" alt="image" src="https://github.com/user-attachments/assets/036a6719-b6bc-4c45-9efb-f7b6c458a409" />

now let add difference between v(ra1)-v(qp2)

<img width="510" height="44" alt="image" src="https://github.com/user-attachments/assets/85ef64dd-2336-41bb-b377-894ad67ebe62" />

plot result:

<img width="1295" height="847" alt="image" src="https://github.com/user-attachments/assets/65c9c63b-57b8-4785-af9b-2ba3c75d5c5e" />


meassure the slope of v(ra1)-v(qp2)

plot result:

<img width="1524" height="993" alt="image" src="https://github.com/user-attachments/assets/afd4a21a-ebe9-479a-a04b-18585f7186c0" />


<img width="575" height="95" alt="image" src="https://github.com/user-attachments/assets/2477f686-c514-487a-88cf-dd843c8e16f3" />

now we check the current

ngspice command: plot vid1#branch vid2#branch

<img width="335" height="50" alt="image" src="https://github.com/user-attachments/assets/07f28e88-e8d4-4f44-bbe6-5784b6137a4c" />

plot result:

<img width="1223" height="938" alt="image" src="https://github.com/user-attachments/assets/484c01dc-b80c-4291-a099-8f07d53a1f58" />


###	BGR (ideal) design and prelayout simulation
&nbsp;

next we simulate below circuit

<img width="1119" height="732" alt="image" src="https://github.com/user-attachments/assets/8ed686a9-4c21-4c08-958f-c2c1549fbd47" />


netlist look like this:

``` spice

**** bgr using ideal opamp (vcvs) *****

.lib "/home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice tt"

.global vdd gnd
.temp 27

*** vcvs definition
e1 net2 gnd ra1 qp1 gain=1000


xmp1    qp1     net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp2    ra1     net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp3    ref     net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4

*** bjt definition
xqp1    gnd     gnd     qp1             sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1
xqp2    gnd     gnd     qp2          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=8
xqp3    gnd     gnd     qp3          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1

*** high-poly resistance definition
xra1    ra1     na1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra2    na1     na2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra3    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xra4    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8

xrb1    ref     nb1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb2    nb1     nb2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb3    nb2     nb3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb4    nb3     nb4     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb5    nb4     nb5     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb6    nb5     nb6     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb7    nb6     nb7     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb8    nb7     nb8     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb9    nb8     nb9     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb10   nb9     nb10    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb11   nb10    nb11    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb12   nb11    nb12    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb13   nb12    nb13    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb14   nb13    nb14    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb15   nb14    nb15    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb16   nb15    nb16    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb17   nb16    nb17    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb18   nb17    nb18    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb19   nb18    nb19    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb20   nb19    nb20    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41        l=7.8
xrb21   nb20    nb21    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb22   nb21    nb22    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41        l=7.8
xrb23   nb22    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8
xrb24   nb22    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41       l=7.8

*** voltage source for current measurement

*** supply voltage
vsup    vdd     gnd     dc      2
*.dc    vsup    0       3.3     0.3.3

.dc     temp    -40     125     5

*vsup    vdd     gnd     pulse   0       2       10n     1u      1u      1m      100u
*.tran   5n      10u

.control
RUN
plot v(vdd) v(qp1) v(ra1) v(qp2) v(ref) v(qp3)
plot v(ref)


.endc
.end
```

run simulation with ngspice:

<img width="804" height="520" alt="image" src="https://github.com/user-attachments/assets/1bd61297-97ff-4cc5-bd2b-abbeefd0a95e" />


plot results:

<img width="1208" height="774" alt="image" src="https://github.com/user-attachments/assets/eaa7bb01-71cb-4ac7-8ca7-4645d849c5da" />


<img width="1370" height="886" alt="image" src="https://github.com/user-attachments/assets/029849cd-3be3-4edd-a4ca-5ec7731aa84f" />


###	Complete BGR design and prelayout simulation
&nbsp;

next we are using bgr_lvt_rpolyh_3p40_ss.sp spice file for simulation of complet BGR design

bgr_lvt_rpolyh_3p40_ss.sp:

``` spice
**** bandgap reference circuit using self-biase current mirror at ss corner****

.lib "/home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice ss"

.global vdd gnd
.temp 27

*** circuit definition ***

*** mosfet definitions self-biased current mirror and output branch
xmp1    net1    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp2    net2    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp3    net3    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmn1    net1    net1    q1      gnd     sky130_fd_pr__nfet_01v8_lvt     l=1     w=5     m=8
xmn2    net2    net1    q2      gnd     sky130_fd_pr__nfet_01v8_lvt     l=1     w=5     m=8

*** start-upcircuit
xmp4    net4    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=1
xmp5    net5    net2    net4    vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=1
xmp6    net7    net6    net2    vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=2
xmn3    net6    net6    net8    gnd     sky130_fd_pr__nfet_01v8_lvt     l=7     w=1     m=1
xmn4    net8    net8    gnd     gnd     sky130_fd_pr__nfet_01v8_lvt     l=7     w=1     m=1

*** bjt definition
xqp1    gnd     gnd     qp1             sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1
xqp2    gnd     gnd     qp2          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=8
xqp3    gnd     gnd     qp3          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1

*** high-poly resistance definition
xra1    ra1     na1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra2    na1     na2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra3    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra4    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8

xrb1    vref    nb1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb2    nb1     nb2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb3    nb2     nb3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb4    nb3     nb4     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb5    nb4     nb5     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb6    nb5     nb6     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb7    nb6     nb7     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb8    nb7     nb8     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb9    nb8     nb9     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb10   nb9     nb10    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb11   nb10    nb11    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb12   nb11    nb12    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb13   nb12    nb13    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb14   nb13    nb14    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb15   nb14    nb15    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb16   nb15    nb16    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb17   nb16    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb18   nb16    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8

*** voltage source for current measurement
vid1    q1      qp1     dc      0
vid2    q2      ra1     dc      0
vid3    net3    vref    dc      0
vid4    net7    net1    dc      0
vid5    net5    net6    dc      0

*** supply voltage
vsup    vdd     gnd     dc      2
*.dc    vsup    0       3.3     0.3.3
.dc     temp    -40     125     5

*vsup   vdd     gnd     pulse   0       2       10n     1u      1u      1m      100u
*.tran  5n      10u

.control
run

plot v(vdd) v(net1) v(net2) v(qp1) v(ra1) v(qp2) v(vref) v(qp3)
plot vid1#branch vid2#branch vid3#branch vid4#branch vid5#branch

.endc
.end

```

run spice simulation:

<img width="865" height="486" alt="image" src="https://github.com/user-attachments/assets/64abcf3f-6b68-4f1e-8fa8-770f686d6c2d" />

plot results:

<img width="1473" height="868" alt="image" src="https://github.com/user-attachments/assets/c42d96a0-d348-4823-8fc0-00ce10730f7d" />


<img width="1351" height="864" alt="image" src="https://github.com/user-attachments/assets/44fb2971-767e-48e7-8699-a4f888925f6c" />


plot v(vref)

<img width="1303" height="919" alt="image" src="https://github.com/user-attachments/assets/5889fb66-2c77-4e6e-b4a1-42b86742638f" />


comparing with bgr_lvt_rpolyh_3p40_ff.sp spice file (ff)

``` spice

**** bandgap reference circuit using self-biase current mirror at ff corner*****

.lib "/home/mimo3000n/share/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice ff"

.global vdd gnd
.temp 27

*** circuit definition ***

*** mosfet definitions self-biased current mirror and output branch
xmp1    net1    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp2    net2    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmp3    net3    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=4
xmn1    net1    net1    q1      gnd     sky130_fd_pr__nfet_01v8_lvt     l=1     w=5     m=8
xmn2    net2    net1    q2      gnd     sky130_fd_pr__nfet_01v8_lvt     l=1     w=5     m=8

*** start-upcircuit
xmp4    net4    net2    vdd     vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=1
xmp5    net5    net2    net4    vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=1
xmp6    net7    net6    net2    vdd     sky130_fd_pr__pfet_01v8_lvt     l=2     w=5     m=2
xmn3    net6    net6    net8    gnd     sky130_fd_pr__nfet_01v8_lvt     l=7     w=1     m=1
xmn4    net8    net8    gnd     gnd     sky130_fd_pr__nfet_01v8_lvt     l=7     w=1     m=1

*** bjt definition
xqp1    gnd     gnd     qp1             sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1
xqp2    gnd     gnd     qp2          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=8
xqp3    gnd     gnd     qp3          sky130_fd_pr__pnp_05v5_W3p40L3p40       m=1

*** high-poly resistance definition
xra1    ra1     na1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra2    na1     na2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra3    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xra4    na2     qp2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8

xrb1    vref    nb1     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb2    nb1     nb2     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb3    nb2     nb3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb4    nb3     nb4     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb5    nb4     nb5     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb6    nb5     nb6     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb7    nb6     nb7     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb8    nb7     nb8     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb9    nb8     nb9     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb10   nb9     nb10    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb11   nb10    nb11    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb12   nb11    nb12    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb13   nb12    nb13    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb14   nb13    nb14    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb15   nb14    nb15    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb16   nb15    nb16    vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb17   nb16    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8
xrb18   nb16    qp3     vdd     sky130_fd_pr__res_high_po_1p41     w=1.41  l=7.8

*** voltage source for current measurement
vid1    q1      qp1     dc      0
vid2    q2      ra1     dc      0
vid3    net3    vref    dc      0
vid4    net7    net1    dc      0
vid5    net5    net6    dc      0

*** supply voltage
vsup    vdd     gnd     dc      2
*.dc    vsup    0       3.3     0.3.3
.dc     temp    -40     125     5

*vsup   vdd     gnd     pulse   0       2       10n     1u      1u      1m      100u
*.tran  5n      10u

.control
run

plot v(vdd) v(net1) v(net2) v(qp1) v(ra1) v(qp2) v(vref) v(qp3)
plot vid1#branch vid2#branch vid3#branch vid4#branch vid5#branch

.endc
.end

```

###	Detailed Startup circuit Simulation
&nbsp;

###	Layout of the components
&nbsp;

###	top-level layout extraction lvs postlayout simulation Final
&nbsp;


plot results:

<img width="1311" height="853" alt="image" src="https://github.com/user-attachments/assets/5e22098c-c389-4a3d-86ef-d11b18792df1" />


<img width="1178" height="780" alt="image" src="https://github.com/user-attachments/assets/fc74ae69-8448-4501-8944-2b6083c379c0" />


<img width="1178" height="780" alt="image" src="https://github.com/user-attachments/assets/8e81d353-981e-487a-8b2d-4a6065225e60" />






